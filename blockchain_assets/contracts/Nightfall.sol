// SPDX-License-Identifier: CC0
pragma solidity ^0.8.20;

import "./proof_verification/ProofSystemRouter.sol";
import {
    ERC3525,
    IERC721Receiver,
    IERC721
} from "@erc-3525/contracts/ERC3525.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {IERC1155} from "@openzeppelin/contracts/token/ERC1155/IERC1155.sol";
import {IERC165} from "@openzeppelin/contracts/utils/introspection/IERC165.sol";
import {IERC3525} from "@erc-3525/contracts/IERC3525.sol";
import {IERC1155Receiver} from "@openzeppelin/contracts/token/ERC1155/IERC1155Receiver.sol";
import {IERC3525Receiver} from "@erc-3525/contracts/IERC3525Receiver.sol";

import "./ProposerManager.sol";
import "./X509/Certified.sol";
import "./X509/X509.sol";

import {Initializable} from "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import {UUPSUpgradeable} from "@openzeppelin/contracts-upgradeable/proxy/utils/UUPSUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/utils/ReentrancyGuardUpgradeable.sol";


enum OperationType {
    DEPOSIT,
    WITHDRAW,
    TRANSFER
}
// in entities.rs, we have defined
// TokenType::ERC20 => 0,
// TokenType::ERC1155 => 1,
// TokenType::ERC721 => 2,
// TokenType::ERC3525=> 3,
// So, the following enum order can't be changed
enum TokenType {
    ERC20, // 0
    ERC1155, // 1
    ERC721, // 2
    ERC3525, // 3
    FeeToken //4
}

// This is the format for a transaction that has been processed by a Proposer and rolled up into a block
// Note: fee is needed here, as we don't want proposer to alter some client's fee but keep the total fee unchanged.
// Such as client_1_fee = 1, client_2_fee = 2, if proposer makes client_1_fee = 2, client_2_fee = 1 when it submits data to blockchain, (fee_sum is unchanged, but individual fee is changed), then proposer can get more fee from client_1 than it should get.
// The publicdata hash won't be the same, therefore we have to keep this `fee` in OnChainTransaction
struct OnChainTransaction {
    uint256 fee;
    uint256[4] commitments;
    uint256[4] nullifiers;
    uint256[4] public_data; // compressed_secrets in each client proof
}

struct DepositCommitment {
    uint256 nfTokenid;
    uint256 nfSlotId;
    uint256 value;
    uint256 secretHash;
}
struct DepositFeeState {
    uint256 fee;
    uint8 escrowed;
    uint8 redeemed;
}

struct WithdrawData {
    uint256 nf_token_id;
    address recipient_address;
    uint256 value;
    uint256 withdraw_fund_salt;
}

struct Block {
    uint256 commitments_root;
    uint256 nullifier_root;
    uint256 commitments_root_root;
    OnChainTransaction[] transactions;
    // rollup_proof contains fee_sum for transfers and withdrawals || 2 BN254 accumulators, each includes 1 G1 commitment and 1 G1 proof. || one ultra plonk proof.
    bytes rollup_proof;
    uint256 block_number;
}

struct TokenIdValue {
    address erc_address;
    uint256 token_id;
    TokenType token_type;
}

struct SlotIdValue {
    address erc_address;
    uint256 slot_id;
    TokenType token_type;
}

error escrowFundsError();

/// @title Nightfall (UUPS-upgradeable)
/// @dev Uses `initialize()` (not constructor) and Certified’s `onlyOwner` for auth. `_authorizeUpgrade` gates upgrades.
contract Nightfall is
    Certified,
    UUPSUpgradeable,
    IERC721Receiver,
    IERC165,
    IERC1155Receiver,
    IERC3525Receiver,
    ReentrancyGuardUpgradeable
{
    
    /// @custom:oz-upgrades-unsafe-allow constructor
    constructor() {
        _disableInitializers();
    }

    event BlockProposed(int256 indexed layer2_block_number);
    event DepositEscrowed(uint256 nfSlotId, uint256 value);

    // remember a Deposit's fee
    mapping(uint256 => DepositFeeState) internal feeBinding;
    // remember whether a Withdraw can be actioned
    mapping(bytes32 => uint8) internal withdrawalIncluded;
    // withdrawalIncluded[key] == 1 means this withdraw transaction is in a Layer 2 block and it's onchain
    // withdrawalIncluded[key] == 0 means this withdraw transaction either hasn't showed on chain or there is no fund to withdraw regarding to this withdraw data

    // Map Nightfall tokenId to the original ercAddress and tokenId
    mapping(uint256 => TokenIdValue) internal tokenIdMapping;
    // Map Nightfall slotId to the original ercAddress and slotId
    mapping(uint256 => SlotIdValue) internal slotIdMapping;

    int256 public layer2_block_number; // set in initialize to 0
    uint256 internal commitmentRoot; // set in initialize to 0
    uint256 internal nullifierRoot; // set in initialize to 5626012003977595441102792096342856268135928990590954181023475305010363075697
    uint256 internal historicRootsRoot; // set in initialize to 0

    ProposerManager internal proposer_manager;
    ProofSystemRouter internal router;
    uint256 internal feeId;

    /// @notice Proxy initializer
    function initialize(
        uint256 initialNullifierRoot,
        uint256 initialCommitmentRoot,
        uint256 initialHistoricRootsRoot,
        int256 initialLayer2BlockNumber,
        ProofSystemRouter addr_router,
        address x509_address,
        address sanctionsListAddress
    ) public initializer {
        __UUPSUpgradeable_init();
        __ReentrancyGuard_init();
        __Certified_init(msg.sender, x509_address, sanctionsListAddress);

        nullifierRoot = initialNullifierRoot;

        commitmentRoot = initialCommitmentRoot;
        historicRootsRoot = initialHistoricRootsRoot;
        layer2_block_number = initialLayer2BlockNumber;

        // Wire authorities directly (avoid external self-calls)
        x509 = X509(x509_address);
        sanctionsList = SanctionsListInterface(sanctionsListAddress);

        router = addr_router;

        uint256 computedFeeId;
        assembly {
            // Allocate memory pointer (free memory pointer)
            let ptr := mload(0x40)
            // Store address(this) left-padded to 32 bytes
            mstore(ptr, address())
            // Store uint256(0) just after (32 bytes later)
            mstore(add(ptr, 0x20), 0)
            // Compute keccak256 over the 64 bytes
            computedFeeId := shr(4, keccak256(ptr, 0x40))
        }
        feeId = computedFeeId;
        // nfTokenId for fee commitment is keccak256(abi.encode(address(this), 0))
        tokenIdMapping[feeId] = TokenIdValue(address(this), 0, TokenType.FeeToken);
        // fee slot is also 0 for native fee commitments
        slotIdMapping[feeId] = SlotIdValue(address(this), 0, TokenType.FeeToken);
    }

    function set_x509_address(address x509_address) external onlyOwner {
        x509 = X509(x509_address);
    }

    function set_sanctions_list(
        address sanctionsListAddress
    ) external onlyOwner {
        sanctionsList = SanctionsListInterface(sanctionsListAddress);
    }

    function set_proposer_manager(
        ProposerManager proposer_manager_address
    ) external onlyOwner {
        proposer_manager = proposer_manager_address;
    }

    /***********************************************************************************
     * This function is called by the proposer to submit a new L2 block. It's the main  *
     * entry point to the contract.                                                     *
     ************************************************************************************/
    function propose_block(Block calldata blk) external virtual onlyCertified nonReentrant {
        require(
            proposer_manager.get_current_proposer_address() == msg.sender,
            "Only the current proposer can propose a block"
        );
        require(
            blk.block_number == uint256(layer2_block_number),
            "Nightfall: block number mismatch"
        );

        // Hash the transactions for the public data
        uint256 block_transactions_length;
        assembly {
            block_transactions_length := calldataload(
                add(blk, calldataload(add(blk, 0x60)))
            )
        }

        // block_transactions_length can only be 64 or 256 (each block produces
        // 4 commitments per transaction, so block_transactions_length * 4 must be
        // a multiple of the client/proposer commitment-tree sub-tree capacity of 8,
        // which holds for 64 and 256).
        require(
            block_transactions_length == 64 ||
                block_transactions_length == 256,
            "Nightfall: block_transactions_length must be 64 or 256"
        );

        uint256[] memory transaction_hashes = new uint256[](
            block_transactions_length
        );
        for (uint256 i = 0; i < block_transactions_length; ++i) {
            transaction_hashes[i] = hash_transaction(blk.transactions[i]);
        }

        uint256[] memory transaction_hashes_new = transaction_hashes;
        for (
            uint256 length = block_transactions_length;
            length > 1;
            length >>= 1
        ) {
            for (uint256 i = 0; i < (length >> 1); ++i) {
                // Directly store computed hash in the same array to save memory
                transaction_hashes_new[i] = sha256_and_shift(
                    abi.encodePacked(
                        transaction_hashes_new[i << 1],
                        transaction_hashes_new[(i << 1) + 1]
                    )
                );
            }
        }
        // get the output of verify_rollup_proof
        // total fee is the total fee paid to proposer for transfers and withdraws
        (bool verified, uint256 totalFee) = verify_rollup_proof(
            blk,
            transaction_hashes[0]
        );
        require(verified, "Rollup proof verification failed");
        // now we need to decode the public data for each transaction and do something with it
        for (uint i = 0; i < block_transactions_length; i++) {
            // Now we work out what transaction we're dealing with and dispatch it to an appropriate handler.

            bool is_deposit;
            // if nullifier[0] is 0, then it's a deposit
            assembly {
                is_deposit := iszero(
                    calldataload(
                        add(
                            blk,
                            add(
                                add(
                                    calldataload(add(blk, 0x60)),
                                    mul(i, 0x1A0)
                                ),
                                0xC0
                            )
                        )
                    )
                )
            }
            // for deposit transaction, we need to filter out the appended dummy deposits, and only process the real deposits.
            // a dummy deposit transaction will have dummy compressed_secrets aka. public_data = [0,0,0,0], we only need to check public_data[0] == 0
            if (is_deposit) {
                bool is_dummy_deposit;
                assembly {
                    is_dummy_deposit := iszero(
                        calldataload(
                            add(
                                blk,
                                add(
                                    add(
                                        calldataload(add(blk, 0x60)),
                                        mul(i, 0x1A0)
                                    ),
                                    0x140
                                )
                            )
                        )
                    )
                }
                if (is_dummy_deposit) continue;

                uint256 localTotalFee = totalFee;
                uint256 publicData;
                for (uint j; j < 4; j++) {
                    assembly {
                        publicData := calldataload(
                            add(
                                blk,
                                add(
                                    add(
                                        calldataload(add(blk, 0x60)),
                                        add(0x20, mul(i, 0x1A0))
                                    ),
                                    add(0x120, mul(j, 0x20))
                                )
                            )
                        )
                    }
                    if (publicData == 0) continue;
                    // if it's a deposit, then get the fee from feeBinding
                    localTotalFee += feeBinding[publicData].fee;
                    require(
                        feeBinding[publicData].escrowed == 1 &&
                            feeBinding[publicData].redeemed == 0,
                        "Deposit either has not been escrowed or has already been redeemed"
                    );
                    feeBinding[publicData].redeemed = 1;
                }
                totalFee = localTotalFee;
                continue;
            }

            bool is_withdraw;
            // is_withdraw = (txn.commitments[0] == 0) && (txn.nullifiers[0] != 0)
            assembly {
                is_withdraw := and(
                    iszero(
                        calldataload(
                            add(
                                blk,
                                add(
                                    add(
                                        calldataload(add(blk, 0x60)),
                                        mul(i, 0x1A0)
                                    ),
                                    0x40
                                )
                            )
                        )
                    ),
                    iszero(
                        iszero(
                            calldataload(
                                add(
                                    blk,
                                    add(
                                        add(
                                            calldataload(add(blk, 0x60)),
                                            mul(i, 0x1A0)
                                        ),
                                        0xC0
                                    )
                                )
                            )
                        )
                    )
                )
            }
            if (is_withdraw) {
                uint256 withdraw_fund_salt = blk.transactions[i].nullifiers[0];
                WithdrawData memory data = WithdrawData(
                    uint256(blk.transactions[i].public_data[0]), //nf_token_id
                    address(
                        uint160(uint256(blk.transactions[i].public_data[1]))
                    ), //recipient_address
                    blk.transactions[i].public_data[2],
                    withdraw_fund_salt
                );

                bytes32 key;
                assembly {
                    let memPtr := mload(0x40)
                    // Store token_id at memPtr
                    mstore(memPtr, mload(data)) // data.nf_token_id
                    // Store recipient_address at memPtr + 32, left-padded to 32 bytes
                    mstore(add(memPtr, 32), mload(add(data, 32)))
                    // Store value at memPtr + 64, left-padded to 32 bytes
                    mstore(add(memPtr, 64), mload(add(data, 64)))
                    // Store salt at memPtr + 96
                    mstore(add(memPtr, 96), mload(add(data, 96)))
                    // Now hash over the full 128 bytes
                    key := keccak256(memPtr, 128)
                }

                // the public data (data) here includes the recipient address. When the recipient attempts to
                // withdraw the amount they are due, they will have to provide the same public data so that the
                // same hash is created. The recipient address will therefore be successfully altered by the caller.
                // Thus, if they provide a different address, the call will fail, if not, all they will do is
                // send the funds for the nightful own, paying gas for the privilege.

                require(
                    withdrawalIncluded[key] == 0,
                    "Funds have already withdrawn"
                );
                // we will give money to the recipient_address once the descrow_funds function is called
                withdrawalIncluded[key] = 1;
                continue;
            }
        }
        // Now we update the roots
        commitmentRoot = blk.commitments_root;
        nullifierRoot = blk.nullifier_root;
        historicRootsRoot = blk.commitments_root_root;

        // Pay the proposer totalFee
        address proposer = proposer_manager.get_current_proposer_address();
        (bool success, ) = proposer.call{value: totalFee}("");
        require(success, "Failed to transfer the fee to the proposer");

        emit BlockProposed(layer2_block_number++);
    }

    function supportsInterface(
        bytes4 interfaceId
    ) external pure override returns (bool) {
        return
            interfaceId == type(IERC165).interfaceId ||
            interfaceId == type(IERC3525Receiver).interfaceId ||
            interfaceId == type(IERC721Receiver).interfaceId ||
            interfaceId == type(IERC1155Receiver).interfaceId;
    }

    // Called by the client to escrow funds so that they can make Deposit transactions.
    // Currently there is no way to un-escrow funds. This could be implemented with a timelock.
    // The deposited funds are keyed by the sha256 hash of DepositData. When data
    // is succesfully created its key is pushed into the array PendingDeposits so that
    // deposits can be processed in order.
    // Note that client can deposit extra deposit_fee, so client can pay for other transactions in the future, but this is not compulsory which means deposit_fee can be 0.
    // if msg.value - 2 * fee > 0, then client paid deposit_fee, two DepositData will be created, one for the value deposit, and one for the deposit_fee deposit. msg.value = deposit_fee + 2 * fee in this case, client needs to pay for the value deposit and deposit_fee deposit, that's why we have 2 * fee.
    // otherwise if msg.value = fee, it means client only paid for the value deposit, and no deposit_fee deposit is created.
    function escrow_funds(
        uint256 fee,
        address ercAddress,
        uint256 tokenId,
        uint256 value,
        uint256 secretHash,
        TokenType token_type
    ) external payable virtual onlyCertified nonReentrant {
        uint256 nfTokenId = sha256_and_shift(abi.encode(ercAddress, tokenId));
        tokenIdMapping[nfTokenId] = TokenIdValue(ercAddress, tokenId, token_type);

        uint256 nativeSlotId = tokenId;
        uint256 nfSlotId = nfTokenId;
        if (token_type == TokenType.ERC3525) {
            nativeSlotId = IERC3525(ercAddress).slotOf(tokenId);
            nfSlotId = uint256(
                keccak256(abi.encode(ercAddress, nativeSlotId))
            ) >> 4;
        }
        slotIdMapping[nfSlotId] = SlotIdValue(ercAddress, nativeSlotId, token_type);

        DepositCommitment memory valueCommitment = DepositCommitment(
            nfTokenId,
            nfSlotId,
            value,
            secretHash
        );
        uint256 key = sha256_and_shift(abi.encode(valueCommitment));

        require(
            feeBinding[key].escrowed == 0,
            "Funds have already been escrowed for this Deposit"
        );

        feeBinding[key] = DepositFeeState(fee, 1, 0);

        if (token_type == TokenType.ERC3525) {
            ERC3525(ercAddress).transferFrom(
                msg.sender,
                address(this),
                tokenId
            );
        } else if (token_type == TokenType.ERC1155) {
            IERC1155(ercAddress).safeTransferFrom(
                msg.sender,
                address(this),
                tokenId,
                value,
                ""
            );
        } else if (token_type == TokenType.ERC721) {
            require(value == 0, "ERC721 tokens should have a value of zero");
            IERC721(ercAddress).safeTransferFrom(
                msg.sender,
                address(this),
                tokenId,
                ""
            );
        } else if (token_type == TokenType.ERC20) {            
            require(tokenId == 0, "ERC20 tokens should have a tokenId of 0");
            require(
                IERC20(ercAddress).transferFrom(
                    msg.sender,
                    address(this),
                    value
                ),
                "ERC20 transfer failed"
            );
        } else {
            revert escrowFundsError();
        }

        emit DepositEscrowed(nfSlotId, value);

        require( msg.value == fee || msg.value >= 2 * fee, "Invalid msg.value for fee or top-up" );

        if (msg.value > 2 * fee) {
            uint256 depositFee = msg.value - 2 * fee;
            DepositCommitment memory depositFeeCommitment = DepositCommitment(
                feeId,
                feeId,
                depositFee,
                secretHash
            );
            uint256 depositFeeKey = sha256_and_shift(
                abi.encode(depositFeeCommitment)
            );
            require(
                feeBinding[depositFeeKey].escrowed == 0,
                "Funds have already been escrowed for this fee Deposit"
            );
            feeBinding[depositFeeKey] = DepositFeeState(fee, 1, 0);
            emit DepositEscrowed(feeId, depositFee);
        }
    }

    function onERC721Received(
        address,
        address,
        uint256,
        bytes calldata
    ) external pure override returns (bytes4) {
        return 0x150b7a02;
    }

    function onERC1155Received(
        address,
        address,
        uint256,
        uint256,
        bytes calldata
    ) external pure override returns (bytes4) {
        return 0xf23a6e61;
    }

    function onERC1155BatchReceived(
        address,
        address,
        uint256[] calldata,
        uint256[] calldata,
        bytes calldata
    ) external pure override returns (bytes4) {
        revert("Unsupported by Nightfall");
    }

    function onERC3525Received(
        address,
        uint256,
        uint256,
        uint256,
        bytes calldata
    ) external pure override returns (bytes4) {
        return 0x009ce20b;
    }

    // Function to the the ercAddress and tokenId of a token if the only information you have is the nfTokenId
    // This is useful if someone transfers a Nightfall token to you and you want to know what the underlying token is.
    function getTokenInfo(
        uint256 nfTokenId
    ) external view returns (address ercAddress, uint256 tokenId, TokenType tokenType) {
        TokenIdValue memory tokenData = tokenIdMapping[nfTokenId];
        return (tokenData.erc_address, tokenData.token_id, tokenData.token_type);
    }

    // Function to get original ercAddress and native slotId from nfSlotId.
    function getSlotInfo(
        uint256 nfSlotId
    ) external view returns (address ercAddress, uint256 slotId, TokenType tokenType) {
        SlotIdValue memory slotData = slotIdMapping[nfSlotId];
        return (slotData.erc_address, slotData.slot_id, slotData.token_type);
    }
    
    // Called by the client to remove their funds from escrow, once they've proved they're entitled to them
    // by submitting a Withdraw transaction that is then proved in a block. We used the compressed_secrets,
    // not because they're really required to prove ownership, but because they are different for every commitment
    // and therefore ensure that the public_data_hash is unique.
    function descrow_funds(
        WithdrawData calldata data,
        TokenType token_type
    ) external payable onlyCertified nonReentrant {
        bytes32 key = keccak256(abi.encode(data));
        // ---- CHECKS ----
        require(
            withdrawalIncluded[key] == 1,
            "Either no funds are available to withdraw, or they are already withdrawn"
        );

        // Now that we know the withdraw is present we get the actual erc-address and tokenId from our mapping.
        TokenIdValue memory original = tokenIdMapping[data.nf_token_id];
        if (original.erc_address == address(0)) {
             // ---- EFFECTS ----
            withdrawalIncluded[key] = 0;
             // ---- INTERACTIONS ----
            (bool complete, ) = data.recipient_address.call{value: data.value}(
                ""
            );
            require(complete, "Could not withdraw fee");
            return;
        }

        // Perform token-type-specific checks
        if (token_type == TokenType.ERC721) {
            require(
                data.value == 0,
                "ERC721 tokens should have a value of zero"
            );
        } else if (token_type == TokenType.ERC20) {
            require(
                original.token_id == 0,
                "ERC20 tokens should have a tokenId of 0"
            );
        }
    
        // ---- EFFECTS ----
        // Update state before interacting with external contracts
        withdrawalIncluded[key] = 0;
 
        // ---- INTERACTIONS ----
        // Perform the token transfer based on token type
        if (token_type == TokenType.ERC3525) {
            uint256 id = IERC3525(original.erc_address).transferFrom(
                original.token_id, 
                data.recipient_address,
                data.value);
        } else if (token_type == TokenType.ERC1155) {
            IERC1155(original.erc_address).safeTransferFrom(
                address(this),
                data.recipient_address,
                original.token_id,
                data.value,
                ""
            );
        } else if (token_type == TokenType.ERC721) {
            IERC721(original.erc_address).safeTransferFrom(
                address(this),
                data.recipient_address,
                original.token_id,
                ""
            );
        } else if (token_type == TokenType.ERC20) {
            require(IERC20(original.erc_address).transfer(data.recipient_address, data.value), "ERC20 Descrow-fund failed"); 
        }
    }

    function sha256_and_shift(
        bytes memory inputs
    ) public pure returns (uint256 result) {
        // assembly {
        //     let freePtr := mload(0x40)
        //     if iszero(
        //         staticcall(
        //             gas(),
        //             0x02,
        //             add(inputs, 0x20),
        //             mload(inputs),
        //             freePtr,
        //             0x20
        //         )
        //     ) {
        //         revert(0, 0)
        //     }
        //     result := shr(4, mload(freePtr))
        // }
        return uint256(sha256(inputs)) >> 4;
    }

    // hashes the public data in a transaction, for use by the rollup proof
    function hash_transaction(
        OnChainTransaction memory txn
    ) public pure returns (uint256) {
        bytes memory concatenatedInputs = abi.encode(
            txn.commitments[0],
            txn.commitments[1],
            txn.commitments[2],
            txn.commitments[3],
            txn.nullifiers[0],
            txn.nullifiers[1],
            txn.nullifiers[2],
            txn.nullifiers[3],
            txn.public_data[0],
            txn.public_data[1],
            txn.public_data[2],
            txn.public_data[3]
        );
        return sha256_and_shift(concatenatedInputs);
    }

    // Verifies the rollup proof
    function verify_rollup_proof(
        Block calldata blk,
        uint256 public_hash
    ) public view returns (bool, uint256) {
        uint8 id = uint8(blk.rollup_proof[0]);
        uint256 feeSumAsNumber;
        uint256[] memory pi;

        if (id == 1) { // PlonkV1
            bytes32 feeSum = bytes32(blk.rollup_proof[1:33]);
            feeSumAsNumber = uint256(feeSum);
            bytes32[] memory publicInputs = new bytes32[](24);
            publicInputs[0] = feeSum;
            publicInputs[1] = bytes32(public_hash);
            publicInputs[2] = bytes32(commitmentRoot);
            publicInputs[3] = bytes32(blk.commitments_root);
            publicInputs[4] = bytes32(nullifierRoot);
            publicInputs[5] = bytes32(blk.nullifier_root);
            publicInputs[6] = bytes32(historicRootsRoot);
            publicInputs[7] = bytes32(blk.commitments_root_root);
            
            uint256[8] memory acc_low;
            uint256[8] memory acc_high;
            (acc_low[0], acc_high[0]) = splitToLowHigh(uint256(bytes32(blk.rollup_proof[33:65])));
            (acc_low[1], acc_high[1]) = splitToLowHigh(uint256(bytes32(blk.rollup_proof[65:97])));
            (acc_low[2], acc_high[2]) = splitToLowHigh(uint256(bytes32(blk.rollup_proof[97:129])));
            (acc_low[3], acc_high[3]) = splitToLowHigh(uint256(bytes32(blk.rollup_proof[129:161])));
            (acc_low[4], acc_high[4]) = splitToLowHigh(uint256(bytes32(blk.rollup_proof[161:193])));
            (acc_low[5], acc_high[5]) = splitToLowHigh(uint256(bytes32(blk.rollup_proof[193:225])));
            (acc_low[6], acc_high[6]) = splitToLowHigh(uint256(bytes32(blk.rollup_proof[225:257])));
            (acc_low[7], acc_high[7]) = splitToLowHigh(uint256(bytes32(blk.rollup_proof[257:289])));

            for (uint i = 0; i < 8; i++) {
                publicInputs[8 + i * 2] = bytes32(acc_low[i]);
                publicInputs[9 + i * 2] = bytes32(acc_high[i]);
            }

            uint256 publicInputsBytes_computed = uint256(
                sha256_and_shift(abi.encodePacked(publicInputs))
            ) % 21888242871839275222246405745257275088548364400416034343698204186575808495617;

            pi = new uint256[](2);
            pi[0] = publicInputsBytes_computed;
            pi[1] = blk.transactions.length;
        } else if (id == 2 || id == 3) { // NovaV1 (single-attestor) or NovaBlsV1 (BLS committee)
            // Both are produced by the Nova prover, so the rollup public inputs
            // are identical. The proof-system-ID byte (2 vs 3) is what routes the
            // verification to the matching on-chain verifier inside router.verify
            // (NovaRollupVerifier for id 2, NovaCommitteeVerifier for id 3).
            // Nova proof doesn't embed feeSum in the proof prefix like Plonk does.
            // Compute the fee sum directly from the transactions.
            feeSumAsNumber = 0;
            for (uint i = 0; i < blk.transactions.length; i++) {
                feeSumAsNumber += blk.transactions[i].fee;
            }

            pi = new uint256[](4);
            pi[0] = blk.commitments_root;
            pi[1] = blk.nullifier_root;
            pi[2] = blk.commitments_root_root;
            pi[3] = blk.transactions.length;
        } else {
            revert("Unknown proof system ID");
        }

        bool is_proof_valid = router.verify(
            blk.rollup_proof,
            pi
        );

        return (
            is_proof_valid,
            feeSumAsNumber
        );
    }

    function splitToLowHigh(
        uint256 value
    ) internal pure returns (uint256 low, uint256 high) {
        // lower 248 bits
        low = value & ((1 << 248) - 1);
        // upper 8 bits
        high = value >> 248;
    }

    // Function that can be called to see if funds are able to be de-escrowed following a withdraw transaction.
    function withdraw_processed(
        WithdrawData calldata data
    ) public view returns (bool) {
        bytes32 key = keccak256(abi.encode(data));
        return withdrawalIncluded[key] == 1;
    }

    // --- UUPS guard ---
    function _authorizeUpgrade(address) internal override onlyOwner {}

    uint256[50] private __gap;
}
